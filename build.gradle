
buildscript {
    ext.guavaVersion = '30.1.1'
}

plugins {
    id 'org.springframework.boot' version '2.6.5' apply false
    id "io.spring.dependency-management" version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '3.2.0' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'application'

    group = 'com.github.poad.examples'
    version = '1.0.0'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        // Use jcenter for resolving dependencies.
        // You can declare any Maven/Ivy/file repository here.
        jcenter()
        mavenCentral()
    }

    def defaultEncoding = 'UTF-8'
    compileJava {
        options.encoding = defaultEncoding
    }
    compileTestJava {
        options.encoding = defaultEncoding
    }
}

project(":app") {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.cloud.tools.jib'

    configurations {
        all*.exclude module: 'spring-boot-starter-logging'
    }

    dependencies {
        implementation "com.google.guava:guava:${guavaVersion}-jre"
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'jakarta.validation:jakarta.validation-api'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation files('libs/MeCab.jar')

        implementation 'io.micrometer:micrometer-registry-prometheus'
        implementation 'io.micrometer:micrometer-registry-jmx'
        implementation 'io.micrometer:micrometer-registry-cloudwatch'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

    jib.from.image = 'docker://docker-java-mecab:latest'
    jib.container.creationTime = 'USE_CURRENT_TIMESTAMP'
    jib.container.ports = [ '8080' ]
    jib.to.image = 'mecab-java-api-server:latest'
}